plugins {
    id "java"
    id "jacoco"
    id "io.micronaut.application"  version "1.5.0"

    id "org.sonarqube" version "3.1.1"
    id "com.diffplug.spotless" version "5.11.0"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

group = groupId
version = artifactVersion

sourceCompatibility = 11
targetCompatibility = 11

spotless {
    java {
        encoding "UTF-8"
        removeUnusedImports()
        eclipse().configFile "${projectDir}/config/codestyle.xml"
    }
}

micronaut {
    version "2.5.0"
    testRuntime "junit5"
    processing {
        incremental true
        module project.name
        group project.group
        annotations "io.micronaut.aws.*"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.20"

    annotationProcessor "org.projectlombok:lombok:1.18.20"
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-graal"

    compileOnly "org.graalvm.nativeimage:svm"

    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    compile "org.jetbrains:annotations:20.1.0"

    compile "io.micronaut:micronaut-runtime"
    compile "io.micronaut:micronaut-validation"
    compile "io.micronaut.aws:micronaut-function-aws"
    compile "io.micronaut.aws:micronaut-function-aws-custom-runtime"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testCompile "io.micronaut.test:micronaut-test-junit5"
    testCompile "io.micronaut.test:micronaut-test-core"

    testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "org.junit.jupiter:junit-jupiter-params"
}

//noinspection GroovyAssignabilityCheck
run {
    environment([
            "SECURITY_ENABLED": "false",
    ])
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }

    environment([
            "SECURITY_ENABLED"     : "false",
            "USE_INTEGRATION_TESTS": "$useIntegrationTests",
    ])
}

mainClassName = "io.micronaut.aws.AwsLambdaRuntime"
jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
        attributes "Build-Version": buildNumber
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

tasks.withType(Test) {
    reports.html.enabled = false
    reports.junitXml.enabled = false
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

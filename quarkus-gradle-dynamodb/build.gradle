plugins {
    id "java"
    id "jacoco"

    id "io.quarkus" version "2.7.3.Final"
    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    quarkusBom
    compileOnly.extendsFrom(quarkusBom)
    implementation.extendsFrom(quarkusBom)
    annotationProcessor.extendsFrom(quarkusBom)
    testAnnotationProcessor.extendsFrom(quarkusBom)
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21.0").configFile("${rootDir}/config/codestyle.xml")
    }
}

dependencies {
    quarkusBom enforcedPlatform("io.quarkus:quarkus-bom:2.7.3.Final")
    quarkusBom enforcedPlatform("software.amazon.awssdk:bom:2.17.118")

    implementation "io.quarkus:quarkus-resteasy"
    implementation "io.quarkus:quarkus-amazon-lambda"

    implementation "commons-logging:commons-logging:1.2"
    implementation "software.amazon.awssdk:apache-client"
    implementation("software.amazon.awssdk:dynamodb") {
        exclude group: "software.amazon.awssdk", module: "netty-nio-client"
    }

    testImplementation "io.quarkus:quarkus-test-amazon-lambda"

    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.rest-assured:rest-assured"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

quarkusBuild {
    configurations {

    }
    nativeArgs {
        containerBuild = true
        buildImage = "quay.io/quarkus/ubi-quarkus-native-image:22.0.0-java17"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled(true)
        html.destination file("${buildDir}/jacocoHtml")
    }
}

plugins {
    id "java"
    id "jacoco"
    id "application"

    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse().configFile("${rootDir}/config/codestyle.xml")
    }
}

configurations {
    all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds' // check for updates every build
        }
    }
    awssdkBom
    compileOnly.extendsFrom(awssdkBom)
    implementation.extendsFrom(awssdkBom)
    annotationProcessor.extendsFrom(awssdkBom)
    testAnnotationProcessor.extendsFrom(awssdkBom)
}

dependencies {
    awssdkBom enforcedPlatform("software.amazon.awssdk:bom:2.17.118")

    annotationProcessor "io.goodforgod:graalvm-hint-processor:0.14.0-SNAPSHOT"
    annotationProcessor "org.projectlombok:lombok:1.18.22"

    compileOnly "io.goodforgod:graalvm-hint-annotations:0.14.0-SNAPSHOT"
    compileOnly "org.projectlombok:lombok:1.18.22"

    implementation "commons-logging:commons-logging:1.2"
    implementation("software.amazon.awssdk:dynamodb") {
        exclude group: "software.amazon.awssdk", module: "netty-nio-client"
    }

    implementation "io.goodforgod:slf4j-simple-logger:0.11.0-SNAPSHOT"
    implementation "io.goodforgod:simple-lambda:0.16.0-SNAPSHOT"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

mainClassName = "io.goodforgod.simplelambda.LambdaEntrypoint"
jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
        attributes "Build-Time": java.time.OffsetDateTime.now()
    }
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled(true)
        html.destination file("${buildDir}/jacocoHtml")
    }
}

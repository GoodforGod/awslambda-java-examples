plugins {
    id "java"
    id "jacoco"

    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.micronaut.application" version "3.3.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = "io.micronaut.function.aws.runtime.MicronautLambdaRuntime"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse().configFile("${rootDir}/config/codestyle.xml")
    }
}

micronaut {
    version("3.3.4")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
        annotations("io.goodforgod.micronaut.*")
        runtime("lambda")
    }
}

configurations {
    all {
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds" // check for updates every build
        }
    }
    awssdkBom
    compileOnly.extendsFrom(awssdkBom)
    implementation.extendsFrom(awssdkBom)
    annotationProcessor.extendsFrom(awssdkBom)
    testAnnotationProcessor.extendsFrom(awssdkBom)
}

dependencies {
    awssdkBom enforcedPlatform("software.amazon.awssdk:bom:2.17.118")

    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-graal"
    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "org.graalvm.nativeimage:svm"

    implementation "org.slf4j:slf4j-simple:1.7.36"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut.aws:micronaut-function-aws"
    implementation "io.micronaut.aws:micronaut-function-aws-api-proxy"

    implementation "commons-logging:commons-logging:1.2"
    implementation("software.amazon.awssdk:dynamodb") {
        exclude group: "software.amazon.awssdk", module: "netty-nio-client"
    }

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "io.micronaut.test:micronaut-test-core"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled(true)
        html.destination file("${buildDir}/jacocoHtml")
    }
}

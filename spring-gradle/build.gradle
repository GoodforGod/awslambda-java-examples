plugins {
    id "java"
    id "jacoco"

    id "org.springframework.boot" version "2.5.3"
    id "org.springframework.experimental.aot" version "0.10.2"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.graalvm.buildtools.native" version "0.9.2"

    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/release/" }
    maven { url "https://maven.springframework.org/release/" }
}

group = groupId
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

spotless {
    java {
        encoding "UTF-8"
        removeUnusedImports()
        eclipse().configFile "${rootDir}/config/codestyle.xml"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.22"

    annotationProcessor "org.projectlombok:lombok:1.18.22"

    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.experimental:spring-native"
    implementation "org.springframework.experimental:spring-native-configuration:0.10.2"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.cloud:spring-cloud-function-web:3.2.2"
    implementation "org.springframework.cloud:spring-cloud-function-context:3.2.2"
    implementation "org.springframework.cloud:spring-cloud-function-adapter-aws:3.2.2"

    implementation "com.amazonaws:aws-lambda-java-events:3.11.0"
    implementation "com.amazonaws:aws-lambda-java-core:1.2.1"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

bootJar {
    mainClass = "io.aws.lambda.spring.LambdaEntrypoint"
}

bootBuildImage {
    builder = "paketobuildpacks/builder:base"
    buildpacks = ["gcr.io/paketo-buildpacks/java-native-image:5.5.0"]
    environment = [
            "BP_NATIVE_IMAGE": "true"
    ]
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled(true)
        html.destination file("${buildDir}/jacocoHtml")
    }
}
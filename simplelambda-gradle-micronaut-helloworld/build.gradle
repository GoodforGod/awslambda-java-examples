plugins {
    id "java"
    id "jacoco"
    id "application"

    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "6.1.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.micronaut.application" version "3.3.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = "io.goodforgod.aws.lambda.simple.micronaut.MicronautInputLambdaEntrypoint"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

micronaut {
    version("3.3.4")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
        annotations("io.goodforgod.simplelambda.micronaut.*")
    }
}

dependencies {
    annotationProcessor "io.goodforgod:graalvm-hint-processor:0.15.0-SNAPSHOT"
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-graal"

    compileOnly "io.goodforgod:graalvm-hint-annotations:0.15.0-SNAPSHOT"
    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "org.graalvm.nativeimage:svm"

    implementation "io.goodforgod:simple-awslambda-micronaut:0.22.0-SNAPSHOT"
    implementation "io.goodforgod:slf4j-simple-logger:0.13.0"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "io.micronaut:micronaut-inject-java"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse("4.21.0").configFile("${rootDir}/config/codestyle.xml")
    }
}

jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled(true)
        html.destination file("${buildDir}/jacocoHtml")
    }
}

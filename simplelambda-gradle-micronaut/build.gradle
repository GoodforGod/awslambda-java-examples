plugins {
    id "java"
    id "jacoco"
    id "application"

    id "io.micronaut.application" version "2.0.6"
    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "5.14.3"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = groupId
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse().configFile("${projectDir}/config/codestyle.xml")
    }
}

micronaut {
    version("3.1.3")
    processing {
        incremental(true)
        module(project.name)
        group(project.group)
        annotations("io.goodforgod.simplelambda.micronaut.*")
    }
}

dependencies {
    annotationProcessor "io.goodforgod:graalvm-hint-processor:0.10.0-SNAPSHOT"
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-graal"

    compileOnly "io.goodforgod:graalvm-hint-annotations:0.10.0-SNAPSHOT"
    compileOnly "org.projectlombok:lombok:1.18.20"
    compileOnly "org.graalvm.nativeimage:svm"

    implementation "io.goodforgod:simple-lambda-micronaut:0.15.0-SNAPSHOT"
    implementation "io.goodforgod:slf4j-simple-logger:0.10.0-SNAPSHOT"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "io.micronaut:micronaut-inject-java"

    testRuntimeOnly "io.goodforgod:slf4j-simple-logger:0.10.0-SNAPSHOT"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

mainClassName = "io.goodforgod.aws.simplelambda.micronaut.MicronautInputLambdaEntrypoint"
jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
        attributes "Build-Version": buildNumber
    }
}

generateResourceConfigFile {
    outputDirectory = layout.buildDirectory.dir("classes/java/main/META-INF/native-image/native-resources")
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.incremental = true
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled(true)
        html.destination(file("${buildDir}/jacocoHtml"))
    }
}

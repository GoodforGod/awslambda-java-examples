plugins {
    id "java"
    id "jacoco"
    id "application"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = "io.goodforgod.simplelambda.LambdaEntrypoint"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

dependencies {
    annotationProcessor "io.goodforgod:graalvm-hint-processor:1.1.0"
    compileOnly "io.goodforgod:graalvm-hint-annotations:1.1.0"

    implementation "io.goodforgod:slf4j-simple-logger:2.0.0"
    implementation "io.goodforgod:simple-awslambda:1.0.0-SNAPSHOT"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.3"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.3"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": artifactVersion
        attributes "Build-Time": java.time.OffsetDateTime.now()
    }
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.destination file("${buildDir}/jacocoHtml")
    }
}
